/*
 *  EMAFilterTest.cpp
 *
 *  Generates a square wave or noise and prints filtered values
 *
 *  Copyright (C) 2020-2023  Armin Joachimsmeyer
 *  Email: armin.joachimsmeyer@gmail.com
 *
 *  This file is part of Arduino-Utils https://github.com/ArminJo/Arduino-Utils.
 *
 *  Arduino-Utils is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 *  See the GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program. If not, see <http://www.gnu.org/licenses/gpl.html>.
 *
 */

#include <Arduino.h>

#define MEASURE_TIMING
//#define TIMING_OUT_PIN  12
#include "SimpleEMAFilters.hpp"

// If no signal is selected, square wave is taken
//#define TEST_WITH_SINE_INPUT
//#define TEST_WITH_TRIANGLE_INPUT
//#define TEST_WITH_RANDOM_INPUT

#define INPUT_PERIOD_SAMPLES_START 25 // Signal with 50 samples per period to see sines generated by double and triple filters.

// Adjust to see behavior for small values
#define MAXIMUM_INPUT_VALUE 100      // Here the 3 lines for filter5 are almost identical - picture 1, 3, 4 in readme
//#define MAXIMUM_INPUT_VALUE 16385L // Here we get overflows for square wave at `InputValue - Lowpass3` while changing sign
//#define MAXIMUM_INPUT_VALUE 16384L // Here we get overflows for square wave at `InputValue - Lowpass3` while changing from - to + value
//#define MAXIMUM_INPUT_VALUE 16383L // The maximum value without overflows for square wave and fast 16 bit filters
//#define MAXIMUM_INPUT_VALUE 20     // here we see clipping effects due to the limited resolution of the used 16 bit math - picture 2 in readme

// Choose which filter to show - see SimpleEMAFilters.h
#if defined(TEST_WITH_RANDOM_INPUT)
#define FILTER_TO_SHOW      PRINT_LOW_PASS_1_3_5
#elif defined(TEST_WITH_SINE_INPUT) || defined(TEST_WITH_TRIANGLE_INPUT)
//#define FILTER_TO_SHOW      PRINT_ALL_SIMPLE_FILTERS
#define FILTER_TO_SHOW      PRINT_LOW_PASS_16_32
#else
#define FILTER_TO_SHOW      PRINT_SIGNIFICANT_FILTERS   // picture 1 in readme
//#define FILTER_TO_SHOW      PRINT_ALL_3DB_FILTERS   // picture 2 in readme
//#define FILTER_TO_SHOW      PRINT_LOW_PASS_1_TO_8
//#define FILTER_TO_SHOW      PRINT_LOW_PASS_1_3_5_8
//#define FILTER_TO_SHOW      PRINT_LOW_PASS_16_32
#endif

void setup() {

    Serial.begin(115200);
#if defined(__AVR_ATmega32U4__) || defined(SERIAL_PORT_USBVIRTUAL) || defined(SERIAL_USB) /*stm32duino*/|| defined(USBCON) /*STM32_stm32*/|| defined(SERIALUSB_PID) || defined(ARDUINO_attiny3217)
    delay(4000); // To be able to connect Serial monitor after reset or power up and before first print out. Do not wait for an attached Serial Monitor!
#endif

//    Serial.println(F("START " __FILE__ "\r\nUsing library version " VERSION_SIMPLE_EMA_FILTERS " from " __DATE__)));

    // Print caption for Arduino Plotter
    printFiltersCaption(FILTER_TO_SHOW);
}

/*
 * Create a signal with a period of 50, which is equivalent to a frequency of 20 Hz for a 1 kHz sampling frequency.
 * Except for random :-)
 */
void loop() {
    static uint16_t sLoopCount = 0; // 0 to 500
    static uint8_t sPeriodCount = 0; // 0 to 4
    static uint8_t sSamplesPerPeriod = INPUT_PERIOD_SAMPLES_START; // 0 to INPUT_PERIOD_SAMPLES_START (200)
    static uint8_t sSampleCount = 0; // 0 to INPUT_PERIOD_SAMPLES_START (200)
    static int16_t sInput;

    while (sLoopCount < 499) {
        sLoopCount++; // Stop output after 500 prints

#if defined(TEST_WITH_RANDOM_INPUT)
    sInput = random(MAXIMUM_INPUT_VALUE);

#elif defined(TEST_WITH_SINE_INPUT)
    sInput = MAXIMUM_INPUT_VALUE * sin(((float) sPeriodCount * TWO_PI) / sSamplesPerPeriod);

#elif defined(TEST_WITH_TRIANGLE_INPUT)
    if (sPeriodCount < sSamplesPerPeriod / 2) {
        sInput = (((MAXIMUM_INPUT_VALUE * 2) * sPeriodCount) / (sSamplesPerPeriod / 2)) - MAXIMUM_INPUT_VALUE;
    } else {
        sInput = (3 * MAXIMUM_INPUT_VALUE) - (((MAXIMUM_INPUT_VALUE * 2) * sPeriodCount) / (sSamplesPerPeriod / 2));
    }
#else
        if (sSampleCount < sSamplesPerPeriod / 2) {
            sInput = MAXIMUM_INPUT_VALUE;
        } else {
            sInput = -MAXIMUM_INPUT_VALUE;
        }
#endif

        doFiltersStep(sInput);
        sSampleCount++;
        if (sSampleCount >= sSamplesPerPeriod) {
            sSampleCount = 0;
            sPeriodCount++;
            if(sPeriodCount >= 2){
                sPeriodCount = 0;
                sSamplesPerPeriod += sSamplesPerPeriod;
            }
        }

#if defined(TEST_WITH_RANDOM_INPUT)
    // print every value additionally 3 times to make results more visible
    for (int i = 0; i < 3; ++i) {
        printFiltersResults(FILTER_TO_SHOW);
    }
#endif
        printFiltersResults(FILTER_TO_SHOW);
    }
    delay(10);
}
